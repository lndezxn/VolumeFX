#version 430

layout(local_size_x = 8, local_size_y = 8, local_size_z = 8) in;

layout(binding = 0) uniform sampler3D u_Vel;
layout(r16f, binding = 0) uniform writeonly image3D u_Div;

uniform ivec3 u_Size;

void main() {
    ivec3 id = ivec3(gl_GlobalInvocationID);
    if (any(greaterThanEqual(id, u_Size))) {
        return;
    }

    vec3 sizef = vec3(u_Size);
    vec3 uv = (vec3(id) + 0.5) / sizef;

    float hx = 1.0 / sizef.x;
    float hy = 1.0 / sizef.y;
    float hz = 1.0 / sizef.z;

    vec3 vL = texture(u_Vel, uv - vec3(hx, 0.0, 0.0)).xyz;
    vec3 vR = texture(u_Vel, uv + vec3(hx, 0.0, 0.0)).xyz;
    vec3 vB = texture(u_Vel, uv - vec3(0.0, hy, 0.0)).xyz;
    vec3 vT = texture(u_Vel, uv + vec3(0.0, hy, 0.0)).xyz;
    vec3 vD = texture(u_Vel, uv - vec3(0.0, 0.0, hz)).xyz;
    vec3 vU = texture(u_Vel, uv + vec3(0.0, 0.0, hz)).xyz;

    float div = 0.5 * (
        (vR.x - vL.x) / hx +
        (vT.y - vB.y) / hy +
        (vU.z - vD.z) / hz);

    imageStore(u_Div, id, vec4(div, 0.0, 0.0, 1.0));
}
