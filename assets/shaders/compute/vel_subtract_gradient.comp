#version 430

layout(local_size_x = 8, local_size_y = 8, local_size_z = 8) in;

layout(binding = 0) uniform sampler3D u_VelIn;
layout(binding = 1) uniform sampler3D u_Pressure;
layout(rgba16f, binding = 0) uniform writeonly image3D u_VelOut;

uniform ivec3 u_Size;

void main() {
    ivec3 id = ivec3(gl_GlobalInvocationID);
    if (any(greaterThanEqual(id, u_Size))) {
        return;
    }

    vec3 sizef = vec3(u_Size);
    vec3 uv = (vec3(id) + 0.5) / sizef;

    float hx = 1.0 / sizef.x;
    float hy = 1.0 / sizef.y;
    float hz = 1.0 / sizef.z;

    float pL = texture(u_Pressure, uv - vec3(hx, 0.0, 0.0)).r;
    float pR = texture(u_Pressure, uv + vec3(hx, 0.0, 0.0)).r;
    float pB = texture(u_Pressure, uv - vec3(0.0, hy, 0.0)).r;
    float pT = texture(u_Pressure, uv + vec3(0.0, hy, 0.0)).r;
    float pD = texture(u_Pressure, uv - vec3(0.0, 0.0, hz)).r;
    float pU = texture(u_Pressure, uv + vec3(0.0, 0.0, hz)).r;

    vec3 gradP = vec3(
        (pR - pL) * 0.5 / hx,
        (pT - pB) * 0.5 / hy,
        (pU - pD) * 0.5 / hz);

    vec3 vel = texture(u_VelIn, uv).xyz;
    vec3 velOut = vel - gradP;

    imageStore(u_VelOut, id, vec4(velOut, 0.0));
}
