#version 430

layout(local_size_x = 8, local_size_y = 8, local_size_z = 8) in;

layout(binding = 0) uniform sampler3D u_PressureIn;
layout(binding = 1) uniform sampler3D u_Div;
layout(r16f, binding = 0) uniform writeonly image3D u_PressureOut;

uniform ivec3 u_Size;
uniform float u_Alpha;    // usually 1.0
uniform float u_InvBeta;  // usually 1.0/6.0

void main() {
    ivec3 id = ivec3(gl_GlobalInvocationID);
    if (any(greaterThanEqual(id, u_Size))) {
        return;
    }

    vec3 sizef = vec3(u_Size);
    vec3 uv = (vec3(id) + 0.5) / sizef;

    vec3 hx = vec3(1.0 / sizef.x, 0.0, 0.0);
    vec3 hy = vec3(0.0, 1.0 / sizef.y, 0.0);
    vec3 hz = vec3(0.0, 0.0, 1.0 / sizef.z);

    float pL = texture(u_PressureIn, uv - hx).r;
    float pR = texture(u_PressureIn, uv + hx).r;
    float pB = texture(u_PressureIn, uv - hy).r;
    float pT = texture(u_PressureIn, uv + hy).r;
    float pD = texture(u_PressureIn, uv - hz).r;
    float pU = texture(u_PressureIn, uv + hz).r;

    float div = texture(u_Div, uv).r;

    float p = (pL + pR + pB + pT + pD + pU - div * u_Alpha) * u_InvBeta;

    imageStore(u_PressureOut, id, vec4(p, 0.0, 0.0, 1.0));
}
